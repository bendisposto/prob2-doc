buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.6'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.11'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'


repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.2.1'
}

asciidoctor {
    // let jruby-gradle download and install gems
    dependsOn jrubyPrepareGems

    // include the diagram gem
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepareGems.outputDir

    options doctype: 'book'

    backends = ['html5', 'pdf']

    attributes 'source-highlighter'    : 'coderay',
               'coderay-linenums-mode' : 'table',
               toc                     : 'left',
               'imagesdir': 'images',
               icon                    : 'font',
               linkattrs               : true,
               encoding                : 'utf-8'


sources {
  include 'handbook.adoc'
}


    resources {
        from('src/docs/asciidoc') {
             include '**/*.png'
        }
    }
}
